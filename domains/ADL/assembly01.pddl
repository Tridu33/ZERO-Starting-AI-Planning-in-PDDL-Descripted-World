(define (problem assembly-test)
   (:domain assembly)
   (:objects lwing rwing fuselage tail lflaps rflaps
	     engine1 engine2 propeller1 propeller2 linchpin
	     airplane - assembly
	     scaffold - resource)
   (:init (available scaffold)
	  (available linchpin)
	  (available propeller1)
	  (available propeller2)
          (available lflaps)
          (available rflaps)
          (available fuselage)

          (requires lwing scaffold)
	  (requires rwing scaffold)
	  (requires tail scaffold)
	  (requires airplane scaffold)
	  (transient-part linchpin engine1)
	  (transient-part linchpin engine2)
	  (part-of propeller1 engine1)
	  (part-of propeller2 engine2)
	  (part-of engine1 lwing)
	  (part-of engine2 rwing)
	  (part-of lflaps lwing)
	  (part-of rflaps rwing)
	  (part-of lwing airplane)
	  (part-of rwing airplane)
	  (part-of fuselage airplane)
	  (part-of linchpin tail)
	  (part-of tail airplane)

	  (assemble-order linchpin propeller1 engine1)
	  (assemble-order linchpin propeller2 engine2)
	  (remove-order propeller1 linchpin engine1)
	  (remove-order propeller2 linchpin engine2)
	  (assemble-order fuselage lwing airplane)
	  (assemble-order fuselage rwing airplane)
	  (assemble-order fuselage tail airplane))
   (:goal (complete airplane)))

;; (defvar assembly-0-solution*
;;         '((assemble linchpin engine1)
;; 	  (assemble propeller1 engine1)
;; 	  (remove linchpin engine1)
;; 	  (assemble linchpin engine2)
;; 	  (assemble propeller2 engine2)
;; 	  (remove linchpin engine2)
;; 	  (commit scaffold rwing)
;; 	  (assemble engine2 rwing)
;; 	  (assemble rflaps rwing)
;; 	  (release scaffold rwing)
;; 	  (commit scaffold lwing)
;; 	  (assemble engine1 lwing)
;; 	  (assemble lflaps lwing)
;; 	  (release scaffold lwing)
;; 	  (commit scaffold tail)
;; 	  (assemble linchpin tail)
;; 	  (release scaffold tail)
;; 	  (commit scaffold airplane)
;; 	  (assemble fuselage airplane)
;; 	  (assemble lwing airplane)
;; 	  (assemble rwing airplane)
;; 	  (assemble tail airplane)))
